Name "MPPatch"
SetCompressor /SOLID /FINAL lzma
OutFile ..\..\target\mppatch-installer.exe
Icon mppatch-installer.ico

RequestExecutionLevel user
ManifestDPIAware true

VIProductVersion "$%NSIS_FILE_VERSION%"
VIFileVersion "$%NSIS_FILE_VERSION%"
VIAddVersionKey /LANG=0 "OriginalFilename" "MPPatch-Installer_win32_$%NSIS_VERSION%.exe"

!define LANG_ENGLISH 1033
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "MPPatch"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "MPPatch Installer"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "(C) Lymia Kanokawa; available under the MIT License"
VIAddVersionKey /LANG=${LANG_ENGLISH} "Comments" "MPPatch Version $%NSIS_VERSION%"

; Mutex code, from https://nsis.sourceforge.io/Allow_only_one_installer_instance
!define INSTALLERMUTEXNAME "MPPatch NSIS Wrapper / 24d1f759-689d-4707-8fb9-3508574253e7"
!macro SingleInstanceMutex
    System::Call 'KERNEL32::CreateMutex(p0, i1, t"${INSTALLERMUTEXNAME}")?e'
    Pop $0
    IntCmpU $0 183 "" launch launch ; ERROR_ALREADY_EXISTS?
        MessageBox MB_ICONSTOP "MPPatch Installer is already running!"
        Abort
    launch:
!macroend
; End mutex code

Function .onInit
    SetSilent silent
    !insertmacro SingleInstanceMutex
FunctionEnd

Function un.onInit
    !insertmacro SingleInstanceMutex
FunctionEnd

Section "Extract and execute wrapped installer"
    RMDir /r $TEMP\MPPatchInstaller
    SetOutPath $TEMP\MPPatchInstaller

    File ..\..\target\native-image\*

    System::Call 'Kernel32::SetEnvironmentVariable(t, t)i ("NSIS_LAUNCH_MARKER", "018c6bba-54e0-7cf2-b16a-7b6abb9215e0").r0'
    System::Call 'Kernel32::SetEnvironmentVariable(t, t)i ("NSIS_LAUNCH_EXE", "$EXEPATH").r0'
    System::Call 'Kernel32::SetEnvironmentVariable(t, t)i ("NSIS_LAUNCH_TEMPDIR", "$TEMP\MPPatchInstaller").r0'

    ExecWait '"$OUTDIR\mppatch-installer.exe"'

    SetOutPath $TEMP
    RMDir /r $TEMP\MPPatchInstaller
SectionEnd
